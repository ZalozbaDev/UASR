## UASR: Unified Approach to Speech Synthesis and Recognition
## - Data packaging functions for fixpoint recognizer (recfix)
##
## AUTHOR : Frank Duckhorn
## PACKAGE: uasr/scripts/dlabpro/tools/include/recpack_recfix

## Copyright 2013-2016 UASR contributors (see COPYRIGHT file)
## - Chair of Communications Engineering, BTU Cottbus-Senftenberg
## - Fraunhofer IKTS, Dresden
## - Chair of System Theory and Speech Technology, TU Dresden
##
## This file is part of UASR.
##
## UASR is free software: you can redistribute it and/or modify it under the
## terms of the GNU Lesser General Public License as published by the Free
## Software Foundation, either version 3 of the License, or (at your option)
## any later version.
##
## UASR is distributed in the hope that it will be useful, but WITHOUT ANY
## WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
## FOR A PARTICULAR PURPOSE. See the GNU Lesser General Public License for more
## details.
##
## You should have received a copy of the GNU Lesser General Public License
## along with UASR. If not, see <http://www.gnu.org/licenses/>.

#@uasrdoc: ignore

function -PACK_fsts(sName,nrm,itRN)
{
  itRN.ud.nrec .nFST_NUM > if;
		"\n  WARN: Too many recognition networks ($[itRN.ud.nrec]>$[.nFST_NUM])" 0 -WARNING;
  end;
  0 var nU; nU .nFST_NUM < while;
    "$[sName][$[nU]]" nrm itRN nU "B" :nU==0: -PACK_fst;
  nU ++=; end;
}

## @param nrm    Weight normalization factor
## @param itRN   FST
## @param nU     Unit index
## @param sTyp   1. letter: S - Small, B - Big Variant
##               2. letter: w - skip push_weights
## @param bAct   Activate network
function -PACK_fst(sName,nrm,itRN,nU,sTyp,bAct)
{
	data idAux;
	data idAux2;
	fst iRN;
  1 var nXMUL;

  "$[sTyp]  " "2" "left" -VAR_strop "1" "right" -VAR_strop var sTyp2;
  sTyp "1" "left" -VAR_strop var sTyp1;

  nU itRN.ud.nrec < if;
  	itRN NULL nU iRN -copy_ui;
	  sTyp2 "w" != if; iRN 0 iRN -push_weights; end;
  else;
    "" iRN /fst /lsr -addunit;
    0 1 iRN -addstates;
    0 0 0 iRN -addtrans;
    { "" } iRN.os =;
    0 nXMUL =;
    FALSE bAct =;
  end;

	# extract transitions
	data iT;
	:iRN.td["~INI"]: iT -join;
	:iRN.td["~TER"]: iT -join;
	:iRN.td["~TIS"]: iT -join;
	:iRN.td["~TOS"]: iT -join;
	"~LSR" iRN.td -find_comp 0 >= if; :iRN.td["~LSR"]: iT -join; else "~LSR" -type long iT -addcomp; end;
	"~STK" iRN.td -find_comp 0 >= if; :iRN.td["~STK"]: iT -join; else "~STK" -type long iT -addcomp; end;
  iT -is_empty not if;
  	iT 1 iT -sortup; iT 0 iT -sortup;
  end;

	# extract state offset
	data iS; :iRN.sd["~FLG"]: iS =;
  iT -is_empty not if;
  	iT 0 idAux -compress;
	  "" -1 iS -rindex; iS idAux 1 0 idAux2 -gen_index; iS 1 1 iS -delete;
  	idAux2 0 idAux 1 2 idAux -lookup; idAux iS -join;
  else;
    -type double 2 iS -addncomps;
  end;

	# quant states
	data dS;
	:iS[1]: 1 idAux -QUANT_u16; idAux dS -join;
	:iS[2]: 1 idAux -QUANT_u8;  idAux dS -join;
	:iS[0]: 1 idAux -QUANT_u8;  idAux dS -join;
	.nMAX_$[sTyp1]FSTS_NUM dS -reallocate;

	# quant transitions
	data dT;
	:iT["~TER"]: 1           idAux -QUANT_u16; idAux dT -join;
	:iT["~TIS"]: 1           idAux -QUANT_s8;  idAux dT -join;
	:iT["~STK"]: 1           idAux -QUANT_s8;  idAux dT -join;
	:iT["~TOS"]: 1           idAux -QUANT_s16; idAux dT -join;
	:iT["~LSR"]: :32768/nrm: idAux -QUANT_s16; idAux dT -join;
	.nMAX_$[sTyp1]FSTT_NUM dT -reallocate;


	# Pack it
	:iS.nrec*nXMUL: 1 "$[sName].fst.xs"  "u16" -PACK1;
	:iT.nrec*nXMUL: 1 "$[sName].fst.xt"  "u16" -PACK1;
	.nPRNW 0 == if;
		32767 1 "$[sName].fst.prnw" "s16" -PACK1;
	else;
		.nPRNW :32768/nrm: "$[sName].fst.prnw" "s16" -PACK1;
	end;
	bAct    1 "$[sName].fst.active" "u8" -PACK1;
	0       1 "_blank1" "u8" -PACK1;
	dS    0 0 "$[sName].fst.s"   "mix" -PACK;
	dT    0 0 "$[sName].fst.t"   "mix" -PACK;
}

## @param sTyp   1. letter: S - Small, B - Big Variant
function -PACK_sym(sName,idOS,sTyp)
{
	# perpare os
	data dOS;
  idOS -is_empty if;
    .nMAX_$[sTyp]SYM_LEN 1 .nMAX_$[sTyp]SYM_NUM dOS -array;
  else;
    :idOS[0]: .nMAX_$[sTyp]SYM_LEN dOS /force -tconvert;
	  .nMAX_$[sTyp]SYM_NUM dOS -reallocate;
  end;

	dOS   0 0 "sym.$[sName]"  "mix" -PACK;
}


function -PACK_cnf(nrm,itGP)
{
  # Get phoneme id table from itGP
  data dGP;
  1 0 0 :itGP.td["~TIS"]: vmax -fetch ++ zeros dGP =; 0 1 dGP -fill;
  dGP itGP.td 0 "~TIS" itGP.td -find_comp dGP -gen_index { -1 };
  dGP 0 itGP.td "~TOS" itGP.td -find_comp 1 dGP -lookup { -1 };

  # Mark pause + trash
  0 var r; r itGP.os.nrec < while;
    :itGP.os[r,0]==".": :itGP.os[r,0]=="#": || if;
      :dGP.==r: dGP -dmark; 255 0 dGP /mark -fill;
    end;
  r ++=; end;

	# Pack it
  var x;
  "recfix.cnf.mode" .nCNFM_AED "S" -CFG_get_ex x -vset; x 1 "cnf.mode" "u8" -PACK1;
  0       1 "_blank1"  "u8"  -PACK1;
  "recfix.cnf.tad" 0.05 "S" -CFG_get_ex x -vset; x 0 "trg.tad" "s16"  -PACK1;
  "recfix.cnf.ted" 0.75 "S" -CFG_get_ex x -vset; x 0 "trg.ted" "s16"  -PACK1;
  0       1 "_blank2"  "u16" -PACK1;
  dGP 1 .nMAX_MIX_NUM "cnf.phn" "u8" -PACK;
}
