## UASR: Unified Approach to Speech Synthesis and Recognition
## - Data packaging tool for hardware recognizer
##
## AUTHOR : Frank Duckhorn

65536  var nF_WND;
1024   var nF_LOG;
65536  var nDim_LOG;
1      var nF_FFT;
65536  var nF_MEL;

function -PACK_log()
{
	data dLOG;
	-type double 1 .nDim_LOG dLOG -array;
	0 :16/.nF_FFT: dLOG -fill;
	dLOG .ln dLOG =;

	dLOG .nF_LOG dLOG.nrec "pfa.log" "u16" -PACK;
}

function -PACK_mel()
{
	data dMELN;
	data dMELM;
	data dMELZ;

	# TODO: extract data from .__FEA_iPfa => m_lpCnvc (.norm, .z, .a)
	{ 6 6 6 6 6 6 6 6 6 7 8 8 8 8 8 8 8 8 8 8 10 14 18 22 26 30 34 38 42 44 } dMELN =;
	{ 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 } dMELZ =;

	data dL; { 6 6  6  6  6  6  6  6  6  6  8  8  8  8  8  8  8  8  8  8  8 12 16  20  24  28  32  36  40  44 44 } dL =;
	data dO; { 6 9 12 15 18 21 24 27 30 33 37 41 45 49 53 57 61 65 69 73 77 83 91 101 113 127 143 161 181 203 } dO =;
	-type double .nMAX_PFA_DIM .nMAX_MEL_DIM dMELM -array;
	var k;
	0 var i; i dO.dim < while;
		0 k =; k :dL[0,i]: < while;
			:dMELM[i,dO[0,i]-dL[0,i  ]+1+k]=(k+1)/dL[0,i];
		k ++=; end;
		0 k =; k :dL[0,i+1]: < while;
			:dMELM[i,dO[0,i]+dL[0,i+1]-1-k]=(k+1)/dL[0,i+1];
		k ++=; end;
	i ++=; end;

	:1./dMELN: dMELN =;
	dMELN .nF_MEL .nMAX_MEL_DIM                 "pfa.mel.n" "u16" -PACK;
	dMELM .nF_MEL :.nMAX_PFA_DIM*.nMAX_MEL_DIM: "pfa.mel.m" "u16" -PACK;
	dMELZ .nF_MEL .nMAX_MEL_DIM                 "pfa.mel.z" "u16" -PACK;
}

function -PACK_pfa(idWnd)
{
	"uasr.sig.srate" 16000 -CFG_get 1 "pfa.srate" "u32" -PACK1;
	"uasr.pfa.crate" 160   -CFG_get 1 "pfa.crate" "u16" -PACK1;
	"uasr.pfa.wlen"  400   -CFG_get 1 "pfa.wlen"  "u16" -PACK1;
	"uasr.pfa.len"   512   -CFG_get 1 "pfa.len"   "u16" -PACK1;
	"uasr.pfa.dim"   30    -CFG_get 1 "pfa.dim"   "u16" -PACK1;
	2                               1 "pfa.typ"   "u32" -PACK1; # TODO: from uasr.pfa
	idWnd .nF_WND .nMAX_FRAME_DIM     "pfa.wnd"   "u16" -PACK;
	-PACK_log;
	-PACK_mel;
}
